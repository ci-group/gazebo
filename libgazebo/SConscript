#Import variable
Import('*')

env.Append(CPPPATH = '#libgazebo')

#
# Create the pkg-config file
#
def createPkgConfig(target,source, env):
  f = open(str(target[0]), 'wb')
  prefix = source[0].get_contents()
  f.write('prefix=' + prefix + '\n')
  f.write('Name: gazebo\n')
  f.write('Description: Simplified interface to Player\n')
  f.write('Version:' + version + '\n')
  f.write('Requires:\n')
  f.write('Libs: -L' + prefix + '/lib -lgazebo\n')
  f.write('Cflags: -I' + prefix + '/include\n')


#
# setup a special build environment for libgazebo. Do this so we can control
# what libgazebo depends upon
#
myEnv = Environment (
  CC = 'g++',
  CCFLAGS = Split ('-pthread -pipe  -W -Wall -O2'),

  CXXCOMSTR = 'Compiling $TARGET',
  CCCOMSTR = 'Compiling $TARGET',

  SHCXXCOMSTR = 'Compiling $TARGET',
  SHCCCOMSTR = 'Compiling $TARGET',

  SHLINKCOMSTR = 'Linking $TARGET',
  LINKCOMSTR = 'Linking $TARGET',

)

env['BUILDERS']['PkgConfig'] = Builder(action = createPkgConfig)
pkgconfig = env.PkgConfig(target='libgazebo.pc', source=Value(install_prefix))
env.Install(dir=install_prefix+'/lib/pkgconfig', source=pkgconfig)

sources = Split('Server.cc Client.cc Iface.cc IfaceFactory.cc SimIface.cc')
headers = Split('gazebo.h IfaceFactory.hh')

sharedLib = myEnv.SharedLibrary('gazebo', sources)
staticLib = myEnv.StaticLibrary('gazebo', sources)

env.Install(install_prefix+'/lib', sharedLib)
env.Install(install_prefix+'/lib', staticLib)
env.Install(install_prefix+'/include/gazebo', headers)
